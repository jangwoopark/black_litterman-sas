/*************************************************************/
/*THIS CODE REPLICATES THE MAIN RESULTS OF HE LITTERMAN 1999 */
/*************************************************************/

/*INPUT DATA*/
DATA CORRELATION;
	INPUT Australia Canada France Germany Japan UK USA;
	DATALINES;
	1.000 0.488 0.478 0.515 0.439 0.512 0.491
    0.488 1.000 0.664 0.655 0.310 0.608 0.779
    0.478 0.664 1.000 0.861 0.355 0.783 0.668
    0.515 0.655 0.861 1.000 0.354 0.777 0.653
    0.439 0.310 0.355 0.354 1.000 0.405 0.306
    0.512 0.608 0.783 0.777 0.405 1.000 0.652
    0.491 0.779 0.668 0.653 0.306 0.652 1.000
	;
RUN;

DATA MARKET_WEIGHTS;
	INPUT Australia Canada France Germany Japan UK USA;
	DATALINES;
	0.016 0.022 0.052 0.055 0.116 0.124 0.615
	;
RUN;

DATA STDEV;
	INPUT Australia Canada France Germany Japan UK USA;
	DATALINES;
	0.160 0.203 0.248 0.271 0.210 0.200 0.187
	;
RUN;

/************************************************************/
/*START PROC IML TO PERFORM MATRIX OPERATION TO FIND OPT    */
/*PORTFOLIO WEIGHTS FROM BLACK-LITTERMAN MODEL              */
/************************************************************/
PROC IML;
	USE CORRELATION;
	READ ALL INTO RHO;

	USE MARKET_WEIGHTS;
	READ ALL INTO WEQ;

	USE STDEV;
	READ ALL INTO STDEV;

	COV = STDEV` * STDEV # RHO;

	/*EQUILIBRIUM EQUITY RISK PREMIA*/
	DELTA = 2.5; /*RISK AVERSION - PAGE 10 OF HE LITTERMAN*/
	TAU = .05; /*CERTAINTY - PAGE 11 OF HE LITTERMAN*/
	EqRiskPremia =  DELTA # COV * WEQ`;

	/*CHOOSE ONE OF THE FOLLOWING TWO SCENARIOS*/
	/*GERMANY WILL OUTPERFORM EUROPE BY 5%*/
	*P = {0 0 -.295 1 0 -.705 0}; /*MARKET CAP WEIGHT FRANCE AND UK (E.G. FRANCE = .052 / (.052 + .124))*/
	*Q = {.05};
	/*GERMANY WILL OUTPERFORM EUROPE BY 5% AND CANADA OUTPERFORMS US BY 3%*/
	P = {0 0 -.295 1 0 -.705 0, 0 1 0 0 0 0 -1};
	Q = {.05, .03};

	TS = TAU # COV;
	OMEGA = P * TS * P` # I(NROW(Q));

	MBAR_INV = INV(INV(TS) + P` * INV(OMEGA) * P);
	SIGMA_BAR = COV + MBAR_INV;
	MU_BAR = MBAR_INV * (INV(TS) * EqRiskPremia + P` * INV(OMEGA) * Q);

	BLACK_LITTERMAN_WEIGHTS = INV(DELTA # SIGMA_BAR) * MU_BAR;

	CREATE COV FROM COV;
	APPEND FROM COV;

	CREATE OMEGA FROM OMEGA;
	APPEND FROM OMEGA;

	CREATE MBAR_INV FROM MBAR_INV;
	APPEND FROM MBAR_INV;

	CREATE MU_BAR FROM MU_BAR;
	APPEND FROM MU_BAR;

	CREATE ERP FROM EqRiskPremia;
	APPEND FROM EqRiskPremia;

	CREATE BL_WEIGHTS FROM BLACK_LITTERMAN_WEIGHTS;
	APPEND FROM BLACK_LITTERMAN_WEIGHTS;
QUIT;
